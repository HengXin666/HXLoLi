---
authors: Heng_Xin
title: QML æ”¯æŒ Wayland æ¯” QWidget å¥½!
date: 2025-08-13 21:34:56
tags:
    - C++
---

## æœ¬äººæ­£å¼åŠ å…¥ QML åœ£æ•™!!!

æœ€è¿‘é‡æ‹¾ä¹‹å‰å†™çš„éŸ³ä¹æ’­æ”¾å™¨é¡¹ç›®. å› ä¸ºä¸€å¼€å§‹æ˜¯ä½¿ç”¨ QWidget å¼€å‘çš„. å¯æ˜¯æˆ‘å°Šè´µçš„ Arch Linux ä½¿ç”¨çš„æ˜¯ KDE (Wayland) æ¡Œé¢ç³»ç»Ÿ...

<!-- truncate -->

ç»“æœå°±æ˜¯ çª—å£çš„ move ä¸è¡Œ, é¡¶ç½®ä¸è¡Œ, å¯ç©¿é€çª—å£ / é€æ˜çª—å£ / æ— è¾¹çª—å£, éƒ½éš¾æ...

ç½‘ä¸Šå…¨æ˜¯ CSDN çš„èµ¤ğŸ’©æ•™ç¨‹, æ ¹æœ¬å°±ä¸è¡Œ. å¤–ç½‘ä¹Ÿåªçœ‹åˆ°äº†å¤–å›½è€å“¥åœ¨æ— å¥ˆ...

GPT / DS ç›´æ¥ä¸Šå¤§ç‚®, å†™å¾—æ˜¾ç„¶ä¸è·¨å¹³å°è¿˜è¶…å¤æ‚...

QWidget æ— è¾¹çª—å£è¢«æ”¾å¼ƒäº†...

---

> [!TIP]
> ## æœ¬äººæ­£å¼åŠ å…¥ QML åœ£æ•™!!!
>
> è¦æ˜¯ QML æ”¹å« **QTML** å°±æ›´å¥½äº† (ç±»ä¼¼äº js -> ts, æ­¤æ¬¡çš„ T æ˜¯ `type`)

æœ€è¿‘çœ‹ç•ª, æ­Œå¥½å¬... Linux æ²¡æœ‰é…·ç‹—... ä¸ä¹ æƒ¯å…¶ä»–éŸ³ä¹ç¨‹åº... æ­£å¥½å¯ä»¥é‡æ„æˆ‘çš„ ASS Music æ’­æ”¾å™¨~

å¹¶ä¸”å­¦äº† QML ä¹Ÿè¦æ‰¾ä¸ªä¸œè¥¿ç»ƒæ‰‹.

è°çŸ¥é“, è®©æˆ‘å‘ç°äº†!! QML å±…ç„¶é»˜è®¤æœ‰è·¨å¹³å°çš„ ç§»åŠ¨ã€ç¼©æ”¾, è¿™è®©æ— è¾¹çª—å£éå¸¸ç®€å•. å¹¶ä¸”é¼ æ ‡ç©¿é€çª—å£ä¹Ÿéå¸¸ç®€å•!

### 1. é€šç”¨æ— è¾¹çª—å£

```qml [c01-é€šç”¨æ— è¾¹çª—å£]
// https://github.com/HengXin666/HX-Music/blob/main/resources/qml/window/BorderlessWindow.qml
pragma ComponentBehavior: Bound
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Window

Window {
    id: root
    flags: Qt.FramelessWindowHint
    color: "transparent"

    property int bw: 3                  // è¾¹æ¡†åšåº¦
    property bool allowInnerMove: true  // ä¸­é—´åŒºåŸŸæ˜¯å¦å…è®¸æ‹–åŠ¨
    property bool showBorder: true      // æ˜¯å¦æ˜¾ç¤ºè¾¹æ¡† (ä¸å½±å“resizeäº¤äº’)
    property color borderColor: "red"   // è¾¹æ¡†é¢œè‰²
    property Component delegate: Item { // æ­£æ–‡å†…å®¹
        Text {
            text: "è¯·åŠ¡å¿…å®ç° delegate è‡ªç»˜å†…éƒ¨å†…å®¹"
        }
    }
    property Component titleBar: Rectangle { // è‡ªç»˜æ ‡é¢˜æ , å¯ä¸ºnull, å†…éƒ¨éœ€è¦è‡ªå®šä¹‰ height
        height: 30
        color: "#3f3f3f"
        MouseArea {
            anchors.fill: parent
            onDoubleClicked: root.toggleMaximized()
            acceptedButtons: Qt.LeftButton
            // ä¸è¦æ‹–åŠ¨, ç§»åŠ¨é€»è¾‘äº¤ç»™å¤–é¢çš„ DragHandler
        }
        RowLayout {
            anchors.fill: parent
            Label {
                text: root.title
                elide: Label.ElideRight
                horizontalAlignment: Qt.AlignHCenter
                verticalAlignment: Qt.AlignVCenter
                Layout.fillWidth: true
            }
            // æœ€å°åŒ–æŒ‰é’®
            Item {
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                Image {
                    anchors.centerIn: parent
                    width: 16
                    height: 16
                    source: "qrc:/icons/dropdown.svg"
                }
                MouseArea {
                    anchors.fill: parent
                    onClicked: root.showMinimized()
                }
            }

            // æœ€å¤§åŒ–/è¿˜åŸæŒ‰é’®
            Item {
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                Image {
                    anchors.centerIn: parent
                    width: 16
                    height: 16
                    source: root.visibility === Window.Maximized
                            ? "qrc:/icons/restore.svg"   // è¿˜åŸå›¾æ ‡
                            : "qrc:/icons/up.svg"  // æœ€å¤§åŒ–å›¾æ ‡
                }
                MouseArea {
                    anchors.fill: parent
                    onClicked: {
                        if (root.visibility === Window.Maximized)
                            root.showNormal();
                        else
                            root.showMaximized();
                    }
                }
            }

            // å…³é—­æŒ‰é’®
            Item {
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                Image {
                    anchors.centerIn: parent
                    width: 16
                    height: 16
                    source: "qrc:/icons/close.svg"
                }
                MouseArea {
                    anchors.fill: parent
                    onClicked: root.close()
                }
            }
        }
    }

    // æš´éœ²çš„å¼•ç”¨
    property alias delegateRef: mainUI.item
    property alias titleBarRef: titleBarLoader.item

    function toggleMaximized() {
        if ((root.visibility & Window.Maximized) === Window.Maximized) {
            root.showNormal();
        } else {
            root.showMaximized();
        }
    }

    // å†…éƒ¨çŠ¶æ€æ•°æ®
    QtObject {
        id: self
        property int bw: 0 // è¾¹æ¡†åšåº¦ [const]

        Component.onCompleted: {
            if (root.showBorder) {
                // å¦‚æœæ˜¾ç¤ºè¾¹æ¡†, é‚£ä¹ˆå…¨å±æ—¶å€™ä¹Ÿéœ€è¦æ˜¾ç¤ºè¾¹æ¡†, å› æ­¤ä¸åº”è¯¥å±è”½è¾¹æ¡†
                return;
            }
            bw = root.bw;
        }
    }

    // å…¨å±æ—¶å€™æ˜¯å¦éœ€è¦è¾¹è·
    onVisibilityChanged: (val) => {
        if (self.bw === 0) {
            return;
        }
        root.bw = (val & Window.Maximized) === Window.Maximized 
            ? 0 
            : self.bw;
    }

    // é¼ æ ‡åŒºåŸŸä»…ç”¨äºè®¾ç½®æ­£ç¡®çš„å…‰æ ‡å½¢çŠ¶
    MouseArea {
        anchors.fill: parent
        hoverEnabled: true
        cursorShape: {
            const mousePoint = Qt.point(mouseX, mouseY);
            const b = root.bw + 10; // è§’å°ºå¯¸
            if (mousePoint.x < b && mousePoint.y < root.bw)              // å·¦ä¸Š
                return Qt.SizeFDiagCursor;
            if (mousePoint.x >= width - b && mousePoint.y >= height - b) // å³ä¸‹
                return Qt.SizeFDiagCursor;
            if (mousePoint.x >= width - b && mousePoint.y < root.bw)     // å³ä¸Š
                return Qt.SizeBDiagCursor;
            if (mousePoint.x < b && mousePoint.y >= height - b)          // å·¦ä¸‹
                return Qt.SizeBDiagCursor;
            if (mousePoint.x < b || mousePoint.x >= width - b)           // æ°´å¹³
                return Qt.SizeHorCursor;
            if (mousePoint.y < root.bw || mousePoint.y >= height - b)    // ç«–ç›´
                return Qt.SizeVerCursor;
        }
        acceptedButtons: Qt.NoButton // ä¸å¤„ç†å®é™…äº‹ä»¶
    }

    // ç¼©æ”¾
    DragHandler {
        id: resizeHandler
        target: null
        // è¦†ç›–æ•´ä¸ªçª—å£
        onActiveChanged: if (active) {
            const p = resizeHandler.centroid.position;
            const b = root.bw + 10; // è§’å°ºå¯¸
            let e = 0;
            const leftEdge = p.x < b;
            const rightEdge = p.x >= root.width - b;
            const topEdge = p.y < root.bw;
            const bottomEdge = p.y >= root.height - b;

            if (leftEdge) {
                e |= Qt.LeftEdge;
            }
            if (rightEdge) {
                e |= Qt.RightEdge;
            }
            if (topEdge) {
                e |= Qt.TopEdge;
            }
            if (bottomEdge) {
                e |= Qt.BottomEdge;
            }

            if (e !== 0) {
                root.startSystemResize(e);
            } else if (root.allowInnerMove) {
                root.startSystemMove();
            }
        }
    }

    // ç»˜åˆ¶è¾¹æ¡† (ä»…è§†è§‰)
    Rectangle {
        anchors.fill: parent
        color: "transparent"
        border.color: root.showBorder ? root.borderColor : "transparent"
        border.width: root.showBorder ? root.bw : 0
    }

    // æ ‡é¢˜æ  (å¦‚æœæœ‰çš„è¯)
    Loader {
        id: titleBarLoader
        width: parent.width - 2 * root.bw
        x: root.bw
        y: root.bw
        sourceComponent: root.titleBar
    }

    // å†…å®¹åŒº
    Loader {
        id: mainUI
        x: root.bw
        y: titleBarLoader.item ? titleBarLoader.y + titleBarLoader.height : root.bw
        width: root.width - 2 * root.bw
        height: root.height - (titleBarLoader.item ? titleBarLoader.y + titleBarLoader.height + root.bw : 2 * root.bw)
        sourceComponent: root.delegate
    }
}
```

ç®€ç›´ç®€å•åˆ°çˆ†ç‚¸!

- `root.startSystemMove();` å°±å¯ä»¥æ‹–åŠ¨

- `root.startSystemResize(e);` å°±å¯ä»¥è°ƒæ•´å¤§å° (å¬è¯´ macOS ä¸æ”¯æŒ(?))

ä»–ä»¬éƒ½æ˜¯è·¨å¹³å°çš„! ä¸è®º win / linux, x11 / Wayland éƒ½å¯ä»¥.

ç›´æ¥çˆ½é£äº†! ä¸ªäººæ­£å¼åŠ å…¥ QML åœ£æ•™! QWidget ä½ ç®—è€å‡ å•Š!

### 2. é¼ æ ‡ç©¿é€

```qml [c2-é¼ æ ‡ç©¿é€(æ•´ä¸ªçª—å£)]
Window {
    property bool locked: false // æ˜¯å¦å¼€å¯æ•´ä¸ªçª—å£çš„é¼ æ ‡ç©¿é€

    flags: Qt.FramelessWindowHint
         | Qt.WindowStaysOnTopHint
         | Qt.Tool
         | (locked ? Qt.WindowDoesNotAcceptFocus | Qt.WindowTransparentForInput : Qt.NoItemFlags)
}
```

```qml [c2-é¼ æ ‡ç©¿é€(éƒ¨åˆ†çª—å£)-QML]
// QML éƒ¨åˆ†
Window {
    property bool locked: false // æ˜¯å¦å¼€å¯éƒ¨åˆ†çª—å£çš„é¼ æ ‡ç©¿é€

    flags: Qt.FramelessWindowHint
         | Qt.WindowStaysOnTopHint

    function lock() {
        locked = true;
        Qt.callLater(() => {
            // è®¡ç®—ä¸éœ€è¦ç©¿é€çš„ä½ç½®
            const posInRoot = delegateRef.lockButtonRef.mapToItem(delegateRef.delegateRoot, 0, 0);
            WindowMaskUtil.addControlRect(
                posInRoot.x, posInRoot.y,
                delegateRef.lockButtonRef.width,
                delegateRef.lockButtonRef.height
            );
            WindowMaskUtil.setMask(root);
        });
    }
    function unlock() {
        locked = false;
        Qt.callLater(() => {
            WindowMaskUtil.clear(root);
        });
    }
}
```

```cpp [c2-é¼ æ ‡ç©¿é€(éƒ¨åˆ†çª—å£)-C++]
// C++ ä¾§
#include <QObject>
#include <QWindow>
#include <QPolygon>
#include <QRegion>

#include <QDebug>

namespace HX {

class WindowMaskUtil : public QObject {
    Q_OBJECT
public:
    explicit WindowMaskUtil(QObject* parent = nullptr)
        : QObject(parent)
    {}

    Q_INVOKABLE void clear(QWindow* window) {
        _regions = {};
        if (window) {
            window->setMask(_regions);
        }
    }

    Q_INVOKABLE void addControlRect(int x, int y, int width, int height) {
        _regions = QRegion(
            QRect(x, y, width, height)
        );
    }

    Q_INVOKABLE void setMask(QWindow* window) {
        if (!window || _regions.isEmpty()) {
            return;
        }
        // è®¾ç½®çª—å£æ©ç , æ”¯æŒä¸è§„åˆ™çš„
        window->setMask(_regions);
    }
private:
    QRegion _regions;
};

} // namespace HX
```

ä¸è¿‡æˆ‘æœŸæœ›ä¸€ä¸ªé¼ æ ‡å¯ä»¥ç©¿é€, ä½†æ˜¯ä»ç„¶å¯ä»¥æ¥å—åˆ°é¼ æ ‡äº‹ä»¶çš„api... ä¼¼ä¹ä¸è¡Œ, å¾ˆéš¾æ. (Wayland)